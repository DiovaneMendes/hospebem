buildscript {
    ext {
        springBootVersion = '2.0.4.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "org.postgresql:postgresql:42.2.5"
    }
}

plugins {
    id "org.flywaydb.flyway" version "5.1.4"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-devtools:${springBootVersion}"

    compile "com.h2database:h2"
    compile "org.postgresql:postgresql:42.2.5"
    compile('org.springframework.boot:spring-boot-starter-security')

    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.2.RELEASE'

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.21.0"
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
}

repositories {
    mavenCentral()
}

task startFromJar(type: Exec, dependsOn: assemble) {
    description = "Inicia aplicacao em plano de fundo, utilizando o jar gerado pela task :app:assemble"

    commandLine "${rootDir}/scripts/local-test-server", "start", "$buildDir/libs/app.jar", "$buildDir"
}

task stopRunningJar(type: Exec) {
    description = "Para aplicacao que foi iniciada por :app:startFromJar"

    commandLine "${rootDir}/scripts/local-test-server", "stop", "$buildDir/libs/app.jar", "$buildDir"
}

flyway {
    url = System.getenv("SPRING_DATASOURCE_URL") ?: 'jdbc:postgresql://localhost:5432/hospebem'
    user = System.getenv("SPRING_DATASOURCE_USERNAME") ?: 'postgres'
    password = System.getenv("SPRING_DATASOURCE_PASSWORD") ?: 'hospebem'
}

bootRun {
    sourceResources sourceSets.main
    jvmArgs = [
      '-Dspring.profiles.active=dev'
    ]
}

build.mustRunAfter('clean')
